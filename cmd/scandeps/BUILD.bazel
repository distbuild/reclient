load("//transitions:defs.bzl", "cc_platform_binary")

cc_library(
    name = "scandeps_lib",
    srcs = ["scandeps.cc"],
    tags = ["manual"],
    deps = [
        "//api/scandeps:scandeps_cc",
        "//cmd/scandeps/server:scandeps_server_lib",
        "@com_github_gflags_gflags//:gflags",
        "@com_github_grpc_grpc//:grpc++",
        "//pkg/version:version_cc",
    ] + select({
        "@platforms//os:windows": [
            "@com_github_google_glog//:glog",
        ],
        "//conditions:default": [],
    }),
)

# TODO(b/243521521): Make this build with static-libstdc++.
cc_platform_binary(
    name = "scandeps_server",
    cxxopt = select({
        "//:clangscandeps_input_processor_config_windows": ["/std:c++17"],
        "//:clangscandeps_input_processor_config": ["-std=c++17"],
        "//:goma_input_processor_config_windows": ["/std:c++14"],
        "//:goma_input_processor_config": ["-std=c++14"],
        "//conditions:default": [],
    }),
    linkopts = select({
        "@platforms//os:linux": [
            "-Wl,--wrap=getentropy",
        ],
        "//conditions:default": [],
    }),
    tags = [
        "local",
    ],
    target_compatible_with = select({
        "//:clangscandeps_input_processor_config": [],
        "//:goma_input_processor_config": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    visibility = [
        "//tests/integ:__subpackages__",
    ],
    deps =
        select({
            "//:clangscandeps_input_processor_config": [
                ":scandeps_lib",
                #gazelle:ignore
                "//internal/pkg/clangscandepsipservice",
                "//internal/pkg/clangscandepsipservice:include_processor",
            ],
            "//:goma_input_processor_config": [
                ":scandeps_lib",
                #gazelle:ignore
                "//internal/pkg/gomaipservice",
                "//internal/pkg/gomaipservice:include_processor",
            ],
            "//conditions:default": [],
        }),
)
